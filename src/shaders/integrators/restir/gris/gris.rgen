#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_atomic_float : require
#define SCENE_TEX_IDX 6
#include "gris_commons.glsl"

GrisDirectLighting direct_lighting_buffer = GrisDirectLighting(scene_desc.gris_direct_lighting_addr);
PrefixContributions prefix_contributions = PrefixContributions(scene_desc.prefix_contributions_addr);

layout(binding = 4, std430) writeonly buffer OutReservoirs { Reservoir out_reservoirs[]; };
layout(binding = 5, std430) writeonly buffer OutGBuffer { GBuffer out_gbuffers[]; };

uvec4 seed = init_rng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, pc.general_seed);
uvec4 sampling_seed = init_rng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, pc.sampling_seed);

void main() {
	const vec2 pixel = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 in_uv = pixel / vec2(gl_LaunchSizeEXT.xy);
	vec2 d = in_uv * 2.0 - 1.0;
	vec4 origin = ubo.inv_view * vec4(0, 0, 0, 1);
	vec4 target = ubo.inv_projection * vec4(d.x, d.y, 1, 1);
	vec3 direction = vec3(sample_camera(d));
	vec3 col = vec3(0);

	GBuffer gbuffer_out;
	init_gbuffer(gbuffer_out);

	// General variables
	bool last_vertex_specular = false;
	vec3 throughput = vec3(1);
	vec3 direct_lighting = vec3(0);
	float bsdf_pdf_val = 1;
	int depth;

	// GRIS variables
	bool connectable_vertex_found = false;
	bool prev_rough = false;
	bool curr_rough = false;
	Reservoir reservoir;
	GrisData reconnection_data;
	init_reservoir(reservoir);
	init_data(reconnection_data);
	init_data(reservoir.data);

	// Throughput after the reconnection vertex
	vec3 postfix_throughput = vec3(1);
	// Whether the normals at the reconnection vertex has been flipped
	bool rc_side = false;
	// Reconnection pdf times the geometry term at the reconnection vertex
	float rc_partial_jacobian = 1;

	int prefix_depth = 0;
	vec3 prefix_col = vec3(0);
	// Common reconnection data
	uint rc_coords = gl_LaunchIDEXT.x * gl_LaunchSizeEXT.y + gl_LaunchIDEXT.y;
	bool valid_reservoir = false;
	bool connectable = false;
	bool was_connectable = false;

	vec3 postfix_L = vec3(0);
	int postfix_depth = 0;

	// Record initial seed after direct light sampling
	ASSERT(sampling_seed.w == 0);
	reconnection_data.init_seed = sampling_seed.w;

	bool debug_bool = false;

	for (depth = 0;; depth++) {
		traceRayEXT(tlas, flags, 0xFF, 0, 0, 0, origin.xyz, tmin, direction, tmax, 0);
		const bool found_isect = payload.instance_idx != -1;

		if (!found_isect) {
			vec3 atmosphere_L = shade_atmosphere(pc.dir_light_idx, pc.sky_col, origin.xyz, direction, tmax);
			vec3 contribution = throughput * atmosphere_L;
			if (depth > 0) {
				col += contribution;
			} else {
				direct_lighting += contribution;
			}
			if (connectable_vertex_found) {
#if STREAMING_MODE == STREAMING_MODE_INDIVIDUAL
				reconnection_data.rc_Li = postfix_throughput * atmosphere_L;
				reconnection_data.reservoir_contribution = contribution;
				reconnection_data.path_flags =
					pack_path_flags(prefix_depth, depth - prefix_depth, RECONNECTION_TYPE_DEFAULT, false);
				bool merged = stream_reservoir(seed, reservoir, reconnection_data,
											   calc_target_pdf(reconnection_data.reservoir_contribution), 1.0);
				valid_reservoir = valid_reservoir || merged;
#else	// STREAMING_MODE != STREAMING_MODE_INDIVIDUAL
				postfix_L += postfix_throughput * atmosphere_L;
#endif	// STREAMING_MODE == STREAMING_MODE_INDIVIDUAL
			}
			break;
		}

		float triangle_area = 0;
		HitData gbuffer = get_hitdata(payload.attribs, payload.instance_idx, payload.triangle_idx, triangle_area);
		const Material hit_mat = load_material(gbuffer.material_idx, gbuffer.uv);

		const vec3 wo = -direction;
		vec3 n_s = gbuffer.n_s;
		vec3 n_g = gbuffer.n_g;

		bool side = face_forward(n_s, n_g, wo);

		prev_rough = curr_rough;
		curr_rough = is_rough(hit_mat);
		was_connectable = connectable;

		bool far_constaint_satisfied =
			length(gbuffer.pos - origin.xyz) > pc.min_vertex_distance_ratio * pc.scene_extent;
		if (!far_constaint_satisfied) {
			debug_bool = true;
		}
		bool constraints_satisfied = curr_rough && far_constaint_satisfied;

		if (connectable_vertex_found) {
			postfix_depth++;
		} else {
			if (depth > 0) {
				set_bounce_flag(reconnection_data.bounce_flags, depth, constraints_satisfied);
			} else {
				set_bounce_flag(reconnection_data.bounce_flags, 0, curr_rough);
			}
		}

		if (was_connectable && prev_rough && constraints_satisfied) {
			vec2 rc_barycentrics = payload.attribs;
			uvec2 rc_primitive_instance_id = uvec2(payload.triangle_idx, payload.instance_idx);
			reconnection_data.rc_barycentrics = rc_barycentrics;
			reconnection_data.rc_primitive_instance_id = rc_primitive_instance_id;
			reconnection_data.rc_coords = rc_coords;

			connectable_vertex_found = true;
			prefix_depth = depth - 1;
			reconnection_data.rc_wi = direction;
			// Seed is not relevant here
			reconnection_data.rc_seed = -1;
			// Partially fill in the contribution data
			reconnection_data.reservoir_contribution = throughput;
			vec3 to_prev = origin.xyz - gbuffer.pos;
			reconnection_data.rc_partial_jacobian = abs(dot(n_s, wo)) * rc_partial_jacobian / dot(to_prev, to_prev);
		}

		connectable = !connectable_vertex_found && curr_rough;

		if (depth == 0) {
			// Fill in GBuffer
			direct_lighting += throughput * hit_mat.emissive_factor;
			gbuffer_out.barycentrics = payload.attribs;
			gbuffer_out.primitive_instance_id = uvec2(payload.triangle_idx, payload.instance_idx);
		} else {
			float mis_weight = 1.0;
			float g = abs(dot(-direction, n_s)) / (payload.dist * payload.dist);
			float pdf_light_w = 1.0 / (g * triangle_area);
			if (!last_vertex_specular) {
				mis_weight = 1.0 / (1 + pdf_light_w / bsdf_pdf_val);
			}
			vec3 partial_contrib = mis_weight * hit_mat.emissive_factor;

			if (connectable_vertex_found && depth > 1) {
#if STREAMING_MODE == STREAMING_MODE_INDIVIDUAL
				// Stream in the emissive contribution
				// If the previous vertex was the connection vertex, delay MIS computation to the shifting phase
				vec3 partial_contrib_for_rc = (was_connectable ? hit_mat.emissive_factor : partial_contrib);
				reconnection_data.rc_Li = postfix_throughput * partial_contrib_for_rc;
				reconnection_data.reservoir_contribution = partial_contrib_for_rc * throughput;
				if (was_connectable) {
					reconnection_data.reservoir_contribution *= mis_weight;
					reconnection_data.pdf_light_w = pdf_light_w;
				}
				reconnection_data.path_flags = pack_path_flags(
					prefix_depth, depth - prefix_depth,
					was_connectable ? RECONNECTION_TYPE_EMISSIVE_AFTER_RC : RECONNECTION_TYPE_DEFAULT, side);
				bool merged = stream_reservoir(seed, reservoir, reconnection_data,
											   calc_target_pdf(reconnection_data.reservoir_contribution), 1.0);
				valid_reservoir = valid_reservoir || merged;
#else	// STREAMING_MODE != STREAMING_MODE_INDIVIDUAL
				postfix_L += partial_contrib * postfix_throughput;
#endif	// STREAMING_MODE == STREAMING_MODE_INDIVIDUAL
			} else if (depth > 1) {
				prefix_col += partial_contrib * throughput;
			}
			if (depth == 1) {
				direct_lighting += partial_contrib * throughput;
			} else {
				col += partial_contrib * throughput;
			}
		}

		if (depth >= pc.max_depth - 1) {
			break;
		}

		vec3 rc_Li = vec3(0);
		last_vertex_specular = (hit_mat.bsdf_props & BSDF_SPECULAR) != 0;
		// NEE
		if (!last_vertex_specular) {
			uint rc_seed = seed.w;
			uvec4 debug_seed = seed;
			float g_src;
			vec3 light_pos;
			vec2 uv;
			uint instance_idx;
			uint triangle_idx;
			vec3 Li = do_nee(seed, gbuffer.pos, hit_mat, side, n_s, wo, true, g_src, light_pos, uv, instance_idx,
							 triangle_idx);
			vec3 contribution = Li * throughput;

			// At 0 depth, we let ReSTIR DI handle the direct lighting
			bool nee_connectable = depth > 0 && connectable;

			// Stream the NEE path at the reconnection vertex
			if (nee_connectable && any(greaterThan(Li, vec3(0)))) {
				ASSERT(instance_idx != -1);
				// TODO: Optimize
				reconnection_data.rc_barycentrics = uv;
				reconnection_data.rc_primitive_instance_id = uvec2(triangle_idx, instance_idx);
				reconnection_data.rc_coords = rc_coords;
				reconnection_data.rc_partial_jacobian = rc_partial_jacobian;

				reconnection_data.debug_seed = debug_seed;
				reconnection_data.rc_seed = rc_seed;
				reconnection_data.reservoir_contribution = contribution;
				reconnection_data.path_flags = pack_path_flags(depth, 1, RECONNECTION_TYPE_NEE, side);

				reconnection_data.rc_Li = vec3(1000);

				bool merged = stream_reservoir(seed, reservoir, reconnection_data,
											   calc_target_pdf(reconnection_data.reservoir_contribution), 1.0);
				if (merged) {
					set_bounce_flag(reservoir.data.bounce_flags, depth + 1, true);
				}
				valid_reservoir = valid_reservoir || merged;

				if (!valid_reservoir) {
					ASSERT(reservoir.data.path_flags == 0);
				} else {
					ASSERT(reservoir.data.path_flags != 0);
				}

			} else if (connectable_vertex_found) {
#if STREAMING_MODE == STREAMING_MODE_INDIVIDUAL
				reconnection_data.rc_Li = postfix_throughput * Li;
				reconnection_data.reservoir_contribution = contribution;
				reconnection_data.path_flags =
					pack_path_flags(prefix_depth, depth - prefix_depth + 1, RECONNECTION_TYPE_DEFAULT, side);
				bool merged = stream_reservoir(seed, reservoir, reconnection_data,
											   calc_target_pdf(reconnection_data.reservoir_contribution), 1.0);
				valid_reservoir = valid_reservoir || merged;
#else	// STREAMING_MODE != STREAMING_MODE_INDIVIDUAL
				postfix_L += Li * postfix_throughput;
#endif	// STREAMING_MODE == STREAMING_MODE_INDIVIDUAL
			}

			if (depth > 0) {
				col += contribution;
			} else {
				direct_lighting += contribution;
			}
		}
		origin.xyz = offset_ray(gbuffer.pos, n_g);

		// Sample direction
		float cos_theta;

		const vec3 f =
			sample_bsdf(n_s, wo, hit_mat, 1 /*radiance=cam*/, side, direction, bsdf_pdf_val, cos_theta, sampling_seed);
		if (bsdf_pdf_val == 0) {
			break;
		}
		vec3 f_cos = f * abs(cos_theta);
		throughput *= f_cos / bsdf_pdf_val;
		if (connectable_vertex_found) {
			postfix_throughput *= f_cos / bsdf_pdf_val;
		}

		rc_partial_jacobian *= bsdf_pdf_val;
		if (pc.enable_rr == 1) {
			float rr_scale = 1.0;
			if (hit_mat.bsdf_type == BSDF_GLASS) {
				rr_scale *= side ? 1. / hit_mat.ior : hit_mat.ior;
			}
			if (depth > RR_MIN_DEPTH) {
				float rr_prob = min(0.95f, luminance(throughput) * rr_scale);
				if (rr_prob == 0 || rr_prob < rand(seed))
					break;
				else
					throughput /= rr_prob;
			}
		}
	}

	if (isnan(luminance(col))) {
		return;
	}
	if (pc.enable_gris == 1 && connectable_vertex_found) {
#if STREAMING_MODE == STREAMING_MODE_SPLIT
		reconnection_data.rc_Li = postfix_L;
		reconnection_data.reservoir_contribution *= postfix_L;
		reconnection_data.path_flags = pack_path_flags(prefix_depth, postfix_depth, RECONNECTION_TYPE_DEFAULT, rc_side);
		bool merged = stream_reservoir(seed, reservoir, reconnection_data,
									   calc_target_pdf(reconnection_data.reservoir_contribution), 1.0);
		valid_reservoir = valid_reservoir || merged;
#endif	// STREAMING_MODE == STREAMING_MODE_SPLIT
		if (valid_reservoir) {
			uint rc_type;
			uint rc_prefix_length;
			uint rc_postfix_length;
			bool side;
			unpack_path_flags(reservoir.data.path_flags, rc_type, rc_prefix_length, rc_postfix_length, side);
			ASSERT(reservoir.data.path_flags != 0);
			ASSERT(rc_type != RECONNECTION_TYPE_INVALID);

			// LOG_CLICKED3("%d - %d - %d\n", rc_type, rc_prefix_length, rc_postfix_length);
			if (rc_type != RECONNECTION_TYPE_NEE) {
				ASSERT(reservoir_data_valid(reservoir.data));
			}
			// if (rc_type == RECONNECTION_TYPE_EMISSIVE) {
			// 	// LOG_CLICKED2("%d - %d\n", rc_prefix_length, rc_postfix_length);
			// 	uint flag0 = uint(get_bounce_flag(reservoir.data.bounce_flags, 0));
			// 	uint flag1 = uint(get_bounce_flag(reservoir.data.bounce_flags, 1));
			// 	uint flag2 = uint(get_bounce_flag(reservoir.data.bounce_flags, 2));
			// 	LOG_CLICKED4("Len %d : %d - %d - %d\n", rc_prefix_length, flag0, flag1, flag2);
			// }

			uint flag0 = uint(get_bounce_flag(reservoir.data.bounce_flags, 0));
			uint flag1 = uint(get_bounce_flag(reservoir.data.bounce_flags, 1));
			uint flag2 = uint(get_bounce_flag(reservoir.data.bounce_flags, 2));
			if (debug_bool) {
				// LOG_CLICKED3("Len %d : %d - %d\n", rc_prefix_length, rc_type, reservoir.data.bounce_flags);
			}
		}
	}
	if (pc.direct_lighting == 1) {
		col += direct_lighting;
	}

	if (pc.enable_gris == 1) {
		ASSERT(reservoir.w_sum >= reservoir.target_pdf);
		// The samples are additive, therefore set M to 1
		reservoir.M = 1;
		calc_reservoir_W(reservoir);
		prefix_contributions.d[pixel_idx] = prefix_col;
		reservoir.data.seed_helpers = uvec2(pc.general_seed, pc.sampling_seed);
#ifdef CANONICAL_ONLY
		col = prefix_col + reservoir.data.reservoir_contribution * reservoir.W;
		if (pc.direct_lighting == 1) {
			col += direct_lighting;
		}
		if (pc.frame_num > 0) {
			float w = 1. / float(pc.frame_num + 1);
			vec3 old_col = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).xyz;
			imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(mix(old_col, col, w), 1.f));
		} else {
			imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(col, 1.f));
		}
		return;
#endif	// CANONICAL_ONLY
	}

	if (pc.enable_gris == 0) {
		if (pc.frame_num > 0) {
			float w = 1. / float(pc.frame_num + 1);
			vec3 old_col = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).xyz;
			imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(mix(old_col, col, w), 1.f));
		} else {
			imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(col, 1.f));
		}
	} else {
		if (pc.direct_lighting == 1) {
			direct_lighting_buffer.d[pixel_idx] = direct_lighting;
		}
		if (!valid_reservoir) {
			ASSERT(!reservoir_data_valid(reservoir.data));
			ASSERT(reservoir.data.path_flags == 0);
		}

		out_reservoirs[pixel_idx] = reservoir;
		out_gbuffers[pixel_idx] = gbuffer_out;
	}
}