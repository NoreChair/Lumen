#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_atomic_float : require

#define SCENE_TEX_IDX 9

#include "ddgi_commons.h"
#include "../../commons.glsl"
#include "ddgi_commons.glsl"
layout(location = 0) rayPayloadEXT HitPayload payload;
layout(location = 1) rayPayloadEXT AnyHitPayload any_hit_payload;
layout(push_constant) uniform _PushConstantRay { PCDDGI pc; };
layout(buffer_reference, scalar, buffer_reference_align = 4) buffer GBuffer { GBufferData d[]; };
layout(buffer_reference, scalar, buffer_reference_align = 4) buffer ProbeOffset { vec4 d[]; };
const uint flags = gl_RayFlagsOpaqueEXT;
#define RR_MIN_DEPTH 3
uvec4 seed = init_rng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, pc.frame_num);
layout(binding = 4) uniform _DDGIUniforms { DDGIUniforms ddgi_uniforms; };
layout(binding = 5, rgba16f) uniform image2D radiance_img;
layout(binding = 6, rgba16f) uniform image2D dir_dist_img;
layout(binding = 7) uniform sampler2D irr_img;
layout(binding = 8) uniform sampler2D depth_img;
GBuffer gbuffer = GBuffer(scene_desc.g_buffer_addr);
ProbeOffset probe_offsets = ProbeOffset(scene_desc.probe_offsets_addr);
float tmin = ddgi_uniforms.tmin;
float tmax = ddgi_uniforms.tmax;
#include "../pt_commons.glsl"

#include "ddgi_irradiance.glsl"

vec3 grid_coord_to_position(ivec3 c) { return ddgi_uniforms.probe_step * vec3(c) + ddgi_uniforms.probe_start_position; }

ivec3 probe_index_to_grid_coord(int index) {
	ivec3 res;

	res.x = index % ddgi_uniforms.probe_counts.x;
	res.y = (index % (ddgi_uniforms.probe_counts.x * ddgi_uniforms.probe_counts.y)) / ddgi_uniforms.probe_counts.x;
	res.z = index / (ddgi_uniforms.probe_counts.x * ddgi_uniforms.probe_counts.y);

	// Assumes probe_counts are PoT.
#if 0
	res.x = index & (ddgi_uniforms.probe_counts.x - 1);
	res.y =
	    (index &
	     ((ddgi_uniforms.probe_counts.x * ddgi_uniforms.probe_counts.y) - 1))
	     >>
	    findMSB(ddgi_uniforms.probe_counts.x);
	res.z = index >> findMSB(ddgi_uniforms.probe_counts.x *
	                         ddgi_uniforms.probe_counts.y);
#endif
	return res;
}

vec3 probe_location(int index) {
	vec3 offset = probe_offsets.d[index].xyz;
	return offset + grid_coord_to_position(probe_index_to_grid_coord(index));
}

// https://observablehq.com/@meetamit/fibonacci-lattices
vec3 spherical_fibonacci(float i, float n) {
	const float PHI = sqrt(5.0) * 0.5 + 0.5;
#define madfrac(A, B) ((A) * (B)-floor((A) * (B)))
	float phi = TWO_PI * madfrac(i, PHI - 1);
	float cos_theta = 1.0 - (2.0 * i + 1.0) * (1.0 / n);
	float sin_theta = sqrt(clamp(1.0 - cos_theta * cos_theta, 0.0f, 1.0f));
	return vec3(cos(phi) * sin_theta, sin(phi) * sin_theta, cos_theta);
#undef madfrac
}

void main() {
	int ray_id = int(gl_LaunchIDEXT.x);
	int probe_id = int(gl_LaunchIDEXT.y);
	vec3 o = probe_location(probe_id);
	vec3 d = normalize(mat3(pc.probe_rotation) * spherical_fibonacci(ray_id, ddgi_uniforms.rays_per_probe));
	payload.dist = tmax;
	traceRayEXT(tlas, flags, 0xFF, 0, 0, 0, o, tmin, d, tmax, 0);
	const bool found_isect = payload.material_idx != -1;
	vec3 col = vec3(0);
	float dist;
	if (!found_isect) {
		col += pc.sky_col;
		dist = 1e10;
	} else if (payload.hit_kind == gl_HitKindBackFacingTriangleEXT) {
		dist = -0.2 * payload.dist;
	} else {
		dist = payload.dist;
		const Material hit_mat = load_material(payload.material_idx, payload.uv);
		const vec3 wo = -d;
		vec3 n_s = payload.n_s;
		vec3 n_g = payload.n_g;
		bool side = face_forward(n_s, n_g, wo);

		if ((hit_mat.bsdf_props & BSDF_FLAG_SPECULAR) == 0) {
			const float light_pick_pdf = 1. / pc.light_triangle_count;
			col += uniform_sample_light(seed, hit_mat, payload.pos, side, n_s, wo) / light_pick_pdf;
		}
		if (pc.first_frame == 0 && pc.infinite_bounces == 1) {
			vec3 irradiance = sample_irradiance(payload.pos, n_s, -d);
			col += hit_mat.albedo / PI * irradiance;
		}
	}
	imageStore(radiance_img, ivec2(gl_LaunchIDEXT.xy), vec4(col, 0.0f));
	imageStore(dir_dist_img, ivec2(gl_LaunchIDEXT.xy), vec4(d, dist));
}